#lang racket

;a)
;
;假如按照 Louis 的修改,为 complex 安装了到自身的强制过程 complex->complex。当 exp 操作不存在时,比如下面语句
;
;(exp (make-complex-from-real-imag 10 0) (make-complex-from-real-imag 10 0))
;
;使得程序死循环。在 apply-generic 中添加打印一些信息,会不断输出
;
;apply-generic: op = exp, args =((complex rectangular 10 . 0) (complex rectangular 10 . 0))
;apply-generic: op = exp, args =((complex rectangular 10 . 0) (complex rectangular 10 . 0))
;apply-generic: op = exp, args =((complex rectangular 10 . 0) (complex rectangular 10 . 0))
;apply-generic: op = exp, args =((complex rectangular 10 . 0) (complex rectangular 10 . 0))
;apply-generic: op = exp, args =((complex rectangular 10 . 0) (complex rectangular 10 . 0))
;apply-generic: op = exp, args =((complex rectangular 10 . 0) (complex rectangular 10 . 0))
;....
;
;可以看到修改后的程序,以相同的参数不断重复调用 apply-generic,死循环了。
;b)
;
;Louis 并没有纠正相同类型参数强制的问题,反而使得问题更加严重。当没有添加 complex->complex,复数求 exp 时,程序会直接退出,输出错误信息
;
;No method for these types -- APPLY-GENERIC (exp (complex complex))
;
;Louis 的修改反而会引发死循环。
;
;这是修改后,(get-coercion 'complex 'complex) 可以从表格找到强制函数,于是 cond 中的条件 t1->t2 就会成立。于是执行
;
;(apply-generic op (t1->t2 a1) a2)
;
;而 t1->t2 会返回自身, (t1->t2 a1) 结果为 a1。因此就会不断循环执行
;
;(get-coercion 'complex 'complex)
;(apply-generic op (t1->t2 a1) a2)
;(apply-generic op a1 a2)
;
;(get-coercion 'complex 'complex)
;(apply-generic op (t1->t2 a1) a2)
;(apply-generic op a1 a2)
;
;(get-coercion 'complex 'complex)
;(apply-generic op (t1->t2 a1) a2)
;(apply-generic op a1 a2)
;
;(....)
;
;而修改前,并不能从表格中找到 complex 的强制函数,程序退出并输出错误信息。
;c)
;
;为 apply-generic 添加一个判断。
;
;(define (apply-generic op . args)
;  (define (handle-error type-tags)
;    (error "No method for these types -- APPLY-GENERIC"
;       (list op type-tags)))
;  (let ((type-tags (map type-tag args)))
;    (let ((proc (get op type-tags)))
;      (if proc
;          (apply proc (map contents args))
;          (if (= (length args) 2)
;              (let ((type1 (car type-tags))
;                    (type2 (cadr type-tags))
;                    (a1 (car args))
;                    (a2 (cadr args)))
;                (if (equal? type1 type2)  ;; 练习 2.81 -c 添加此判断
;                    (handle-error type-tags)
;                    (let ((t1->t2 (get-coercion type1 type2))
;                          (t2->t1 (get-coercion type2 type1)))
;                      (cond (t1->t2 (apply-generic op (t1->t2 a1) a2))
;                            (t2->t1 (apply-generic op a1 (t2->t1 a2)))
;                            (else (handle-error type-tags))))))
;              (handle-error type-tags))))))


(require "ch2support.scm")
(require (submod "complex_number_data_directed.scm" complex-op))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (attach-tag type-tag contents)
  (cons type-tag contents))

(define (type-tag datum)
  (if (pair? datum)
      (car datum)
      (error "Bad tagged datum -- TYPE-TAG" datum)))

(define (contents datum)
  (if (pair? datum)
      (cdr datum)
      (error "Bad tagged datum -- CONTENTS" datum)))

; 会引起死循环的 apply-generic
;(define (apply-generic op . args)
;  (display "apply-generic: op = ")
;  (display op)
;  (display ", args =")
;  (display args)
;  (newline)
  
;  (define (handle-error type-tags)
;    (error "No method for these types -- APPLY-GENERIC"
;       (list op type-tags)))
;  (let ((type-tags (map type-tag args)))
;    (let ((proc (get op type-tags)))
;      (if proc
;          (apply proc (map contents args))
;          (if (= (length args) 2)
;              (let ((type1 (car type-tags))
;                    (type2 (cadr type-tags))
;                    (a1 (car args))
;                    (a2 (cadr args)))
;                (let ((t1->t2 (get-coercion type1 type2))
;                      (t2->t1 (get-coercion type2 type1)))
;                  (cond (t1->t2 (apply-generic op (t1->t2 a1) a2))
;                        (t2->t1 (apply-generic op a1 (t2->t1 a2)))
;                        (else (handle-error type-tags)))))
;              (handle-error type-tags))))))

;; 练习 2.81-c
(define (apply-generic op . args)
  (define (handle-error type-tags)
    (error "No method for these types -- APPLY-GENERIC"
       (list op type-tags)))
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (if (= (length args) 2)
              (let ((type1 (car type-tags))
                    (type2 (cadr type-tags))
                    (a1 (car args))
                    (a2 (cadr args)))
                (if (equal? type1 type2)  ;; 练习 2.81 -c 添加此判断
                    (handle-error type-tags)
                    (let ((t1->t2 (get-coercion type1 type2))
                          (t2->t1 (get-coercion type2 type1)))
                      (cond (t1->t2 (apply-generic op (t1->t2 a1) a2))
                            (t2->t1 (apply-generic op a1 (t2->t1 a2)))
                            (else (handle-error type-tags))))))
              (handle-error type-tags))))))
          
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (add x y) (apply-generic 'add x y))
(define (sub x y) (apply-generic 'sub x y))
(define (mul x y) (apply-generic 'mul x y))
(define (div x y) (apply-generic 'div x y))
(define (exp x y) (apply-generic 'exp x y))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (install-scheme-number-package)
  (define (tag x)
    (attach-tag 'scheme-number x))
  (put 'add '(scheme-number scheme-number)
       (lambda (x y) (tag (+ x y))))
  (put 'sub '(scheme-number scheme-number)
       (lambda (x y) (tag (- x y))))
  (put 'mul '(scheme-number scheme-number)
       (lambda (x y) (tag (* x y))))
  (put 'div '(scheme-number scheme-number)
       (lambda (x y) (tag (/ x y))))
  (put 'exp '(scheme-number scheme-number)
       (lambda (x y) (tag (expt x y))))
  (put 'make 'scheme-number
       (lambda (x) (tag x)))
  'done)

(define (make-scheme-number n)
  ((get 'make 'scheme-number) n))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (install-rational-package)
  ;; internal procedures
  (define (number x) (car x))
  (define (denom x) (cdr x))
  (define (make-rat n d) 
    (let ((g (gcd n d)))
      (cons (/ n g) (/ d g))))
  (define (add-rat x y)
    (make-rat (+ (* (number x) (denom y))
                 (* (number y) (denom x)))
              (* (denom x) (denom y))))
  (define (sub-rat x y)
    (make-rat (- (* (number x) (denom y))
                 (* (number y) (denom x)))
              (* (denom x) (denom y))))
  (define (mul-rat x y)
    (make-rat (* (number x) (number y))
              (* (denom x) (denom y))))
  (define (div-rat x y)
    (make-rat (* (number x) (denom y))
              (* (denom x) (number y))))
  
  ;; interface to the rest of the system
  (define (tag x) (attach-tag 'rational x))
  (put 'add '(rational rational)
       (lambda (x y) (tag (add-rat x y))))
  (put 'sub '(rational rational)
       (lambda (x y) (tag (sub-rat x y))))
  (put 'mul '(rational rational)
       (lambda (x y) (tag (mul-rat x y))))
  (put 'div '(rational rational)
       (lambda (x y) (tag (div-rat x y))))
  (put 'make 'rational
       (lambda (n d) (tag (make-rat n d))))
  'done)

(define (make-rational n d)
  ((get 'make 'rational) n d))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (install-complex-package)
  (define (make-from-real-imag x y)
    ((get 'make-from-real-imag '(rectangular)) x y))
  (define (make-from-mag-ang r a)
    ((get 'make-from-mag-ang '(polar)) r a))
  
  ;; internal procedures
  (define (add-complex z1 z2)
    (make-from-real-imag (+ (real-part z1) (real-part z2))
                         (+ (imag-part z1) (imag-part z2))))
  (define (sub-complex z1 z2)
    (make-from-real-imag (- (real-part z1) (real-part z2))
                         (- (imag-part z1) (imag-part z2))))
  (define (mul-complex z1 z2)
    (make-from-mag-ang (* (magnitude z1) (magnitude z2))
                       (+ (angle z1) (angle z2))))
  (define (div-complex z1 z2)
    (make-from-mag-ang (/ (magnitude z1) (magnitude z2))
                       (- (angle z1) (angle z2))))
  
  ;; interface to the rest of the system
  (define (tag z) (attach-tag 'complex z))
  (put 'add '(complex complex)
       (lambda (x y) (tag (add-complex x y))))
  (put 'sub '(complex complex)
       (lambda (x y) (tag (sub-complex x y))))
  (put 'mul '(complex complex)
       (lambda (x y) (tag (mul-complex x y))))
  (put 'div '(complex complex)
       (lambda (x y) (tag (div-complex x y))))
  (put 'make-from-real-imag 'complex
       (lambda (x y) (tag (make-from-real-imag x y))))
  (put 'make-from-mag-ang 'complex
       (lambda (r a) (tag (make-from-mag-ang r a))))
  'done)

(define (make-complex-from-real-imag x y)
  ((get 'make-from-real-imag 'complex) x y))
  
(define (make-complex-from-mag-ang r a)
  ((get 'make-from-mag-ang 'complex) r a))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (install-coercion-package)
  (define (scheme-number->scheme-number n) n)
  (define (complex->complex z) z)
  (define (scheme-number->complex n)
    (make-complex-from-real-imag (contents n) 0))
  (put-coercion 'scheme-number 'complex scheme-number->complex)
  (put-coercion 'scheme-number 'scheme-number scheme-number->scheme-number)
  (put-coercion 'complex 'complex complex->complex)
  'done)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(module* main #f
  (install-complex-package)
  (install-rational-package)
  (install-scheme-number-package)
  (install-polar-package)
  (install-rectangular-package)
  (install-coercion-package)

  (add (make-scheme-number 10) (make-scheme-number 5))
  (sub (make-scheme-number 10) (make-scheme-number 5))
  (mul (make-scheme-number 10) (make-scheme-number 5))
  (div (make-scheme-number 10) (make-scheme-number 5))
  
  (add (make-rational 1 2) (make-rational 1 3))
  (sub (make-rational 1 2) (make-rational 1 3))
  (mul (make-rational 1 2) (make-rational 1 3))
  (div (make-rational 1 2) (make-rational 1 3))
  
  (add (make-complex-from-real-imag 10 20) (make-complex-from-mag-ang 10 3.1415926))
  (sub (make-complex-from-real-imag 10 20) (make-complex-from-mag-ang 10 3.1415926))
  (mul (make-complex-from-real-imag 10 20) (make-complex-from-mag-ang 10 3.1415926))
  (div (make-complex-from-real-imag 10 20) (make-complex-from-mag-ang 10 3.1415926))
  
  (add (make-scheme-number 10) (make-complex-from-real-imag 10 20))
  (add (make-complex-from-real-imag 10 20) (make-scheme-number 10))
  
  (exp (make-scheme-number 10) (make-scheme-number 10))
  (exp (make-complex-from-real-imag 10 0) (make-complex-from-real-imag 10 0))
)